<!DOCTYPE html>
<html>
<head>
<style>
    .full-page {
        width:  100%;
        height:  100vh; /* This will make the div take up the full viewport height */
        display: flex;
        flex-direction: column;
        justify-content: center;
        align-items: center;
    }
    .full-page img {
        max-width:  200;
        max-height:  200;
        margin-bottom: 5rem;
    }
    .full-page div{
        display: flex;
        flex-direction: column;
        justify-content: center;
        align-items: center;
    }
</style>
</head>
<body>

<div class="full-page">
    <img src="./Logo.jpeg" alt="Logo">
    <div>
    <h1>Protocol Audit Report</h1>
    <h3>Prepared by: Bizarro</h3>
    </div>
</div>

</body>
</html>

# Table of Contents
- [Table of Contents](#table-of-contents)
- [Protocol Summary](#protocol-summary)
- [Disclaimer](#disclaimer)
- [Risk Classification](#risk-classification)
  - [Scope](#scope)
  - [Roles](#roles)
- [Executive Summary](#executive-summary)
  - [Issues found](#issues-found)
- [Findings](#findings)
  - [High](#high)
    - [\[H-1\] The entire protocol can be subjected to a DoS attack when swapping collateral tokens through `Paraswap`.](#h-1-the-entire-protocol-can-be-subjected-to-a-dos-attack-when-swapping-collateral-tokens-through-paraswap)
    - [\[H-2\] The `PerpetualVault::afterLiquidationExecution` function is missing the allocation for `isPositionClosed`.](#h-2-the-perpetualvaultafterliquidationexecution-function-is-missing-the-allocation-for-ispositionclosed)
  - [Medium](#medium)
    - [\[M-1\] Excess executionFee not getting refunded to the users](#m-1-excess-executionfee-not-getting-refunded-to-the-users)
    - [\[M-2\] Wrong allocation of `executionFee` in `depositInfo`.](#m-2-wrong-allocation-of-executionfee-in-depositinfo)
    - [\[M-3\] User should get redund when positionIsClosed.](#m-3-user-should-get-redund-when-positionisclosed)
  - [Low](#low)
    - [\[L-1\] leverage should have max cap of `50_000` in the initialize function](#l-1-leverage-should-have-max-cap-of-50_000-in-the-initialize-function)
    - [\[L-2\] Use `safeTransfer` or `transfer` in the whole `PerptualVault` contract.](#l-2-use-safetransfer-or-transfer-in-the-whole-perptualvault-contract)
    - [\[L-3\] In `KeeperProxy::_validatePrice` the token price checks are wrong.](#l-3-in-keeperproxy_validateprice-the-token-price-checks-are-wrong)
    - [\[L-4\] unused state variable.](#l-4-unused-state-variable)

# Protocol Summary

The Perpetual Vault Protocol is a decentralized finance (DeFi) system that enables users to engage in leveraged trading on the GMX decentralized exchange. This protocol aims to simplify the process of managing leveraged positions while providing additional features such as automated position management and risk mitigation.

Users deposit USDC into the contract, and withdraw USDC from the contract.  The USDC is used to open long/short positions on GMX perps.  Each vaults represents exactly one perps market at a specified leverage.  For example, we'll have a 1x ETH vault, 2x ETH vault, and 3x ETH vault.  The leverage of each vault will stay consistent from start to finish.  All short positions and long positions greater than 1x leverage involve opening a position on GMX perpetuals.  If the position is 1x long, then we open a spot position by swapping thru any combination of GMX spot or Paraswap.

We implemented a Keeper system that executes actions via an asynchronous series of actions.  We use enumerable sets to map users to their deposits.  Each deposit can only be withdrawn in whole.  

The strategy of signal changes (i.e. going from long to short or from short to long) is determined offchain and executed by the keeper.

# Disclaimer

Bizarro found as many vulnerabilities in the code in the given time period, but holds no responsibilities for the findings provided in this document. A security audit by the team is not an endorsement of the underlying business or product. The audit was time-boxed and the review of the code was solely on the security aspects of the Solidity implementation of the contracts.

# Risk Classification

|            |        | Impact |        |     |
| ---------- | ------ | ------ | ------ | --- |
|            |        | High   | Medium | Low |
|            | High   | H      | H/M    | M   |
| Likelihood | Medium | H/M    | M      | M/L |
|            | Low    | M      | M/L    | L   |

We use the [CodeHawks](https://docs.codehawks.com/hawks-auditors/how-to-evaluate-a-finding-severity) severity matrix to determine severity. See the documentation for more details.

## Scope 

```js
contract/
├── PerpetualVault.sol
├── GmxProxy.sol
├── KeeperProxy.sol
├── VaultReader.sol
├── interfaces
│   ├── IGmxProxy.sol
│   ├── IPerpetualVault.sol
│   └── IVaultReader.sol
└── libraries
    ├── gmx/
        └──MarketUtils.sol
    ├── Errors.sol
    ├── Order.sol
    ├── ParaSwapUtils.sol
    ├── Position.sol
    └── StructData.sol
```

## Roles
# Executive Summary
## Issues found
 | Severity | Number of issues found |
 | -------- | ---------------------- |
 | High     | 2 |
 | Medium   | 3 |
 | Low      | 4 |
 | Info     | 0 |
 | Gas      | 0 |
 | Total    | 9 |

# Findings
## High
### [H-1] The entire protocol can be subjected to a DoS attack when swapping collateral tokens through `Paraswap`.

**Description:** In `PerpetualVault` contract, users can deposit collateral tokens to receive shares and later withdraw their collateral along with profits, minus fees. However, if the `runNextFlow` function is called after depositing collateral tokens, the protocol becomes stuck in the `FLOW.DEPOSIT` state after swapping tokens, preventing further operations.

**Proof of Concept:** 
1. requirements: positionIsClosed == false, beenLong == true and leverage is 1x.
2. User deposits some collateral tokens.
3. flow is set as `FLOW.DEPOSIT`.
4. as the positionIsClosed is false `nextAction.selector` is set to `NextActionSelector.INCREASE_ACTION`.
5. `runNextAction` is called by the keeper with metadata of length 1 as the `PROTOCOL.DEX`.
6. `if (_nextAction.selector == NextActionSelector.INCREASE_ACTION)` and `if (_isLongOneLeverage(_isLong))` conditions are correct.
7. `_runSwap(metadata, true, prices)` function will be called to swap collateral token for index token.
8. `if (metadata.length == 2)` this function is false so the else statement will be called.
9. `if (_protocol == PROTOCOL.DEX)` and `if (flow == FLOW.DEPOSIT)` are true so after swapping tokens the `_mint()` function is called and the necessary shares are allocated to the depositer.
10. Then the function returns true.

As seen above, the flow is never deleted, causing the protocol to remain stuck in the `FLOW.DEPOSIT` state, which results in a denial of service (DoS) for the entire contract.

**Impact:** Users can inadvertently cause a denial of service (DoS) across the entire protocol, or an attacker can intentionally exploit this issue, preventing other users from depositing or withdrawing collateral tokens.

**Proof of Concept(Coded):** Add this function to the `PerpetualVault.t.sol` and run `forge test --mt test_swappingWillBeDoS --via-ir --rpc-url arbitrum -vvv` The function will revert with a `FlowInProgress()` error, causing the protocol to become unresponsive and preventing users from interacting with it.

```js
  function test_swappingWillBeDoS() public {
    address keeper = PerpetualVault(vault).keeper();
    address alice = makeAddr("alice");
    vm.deal(alice, 10 ether);
    depositFixture(alice, 1e10);

    MarketPrices memory prices = mockData.getMarketPrices();
    bytes memory paraSwapData = mockData.getParaSwapData(vault);
    bytes[] memory swapData = new bytes[](1);
    swapData[0] = abi.encode(PROTOCOL.DEX, paraSwapData);
    vm.prank(keeper);
    PerpetualVault(vault).run(true, true, prices, swapData);
    
    assertEq(uint8(PerpetualVault(vault).flow()), 0);
    assertEq(PerpetualVault(vault).positionIsClosed(), false);
    (PerpetualVault.NextActionSelector selector, ) = PerpetualVault(vault).nextAction();
    assertEq(uint8(selector), 0);
    // here the storage variable will be positionIsClose == false, beenLong == true and leverage == 10_000

    // deposit collateral
    depositFixture(alice, 1e10);

    // nextAction.selector is set to INCREASE_ACTION
    (selector, ) = PerpetualVault(vault).nextAction();
    assertEq(uint8(selector), 1);
    
    // set the metaData for paraswap swap.
    prices = mockData.getMarketPrices();
    paraSwapData = mockData.getParaSwapData(vault);
    swapData = new bytes[](1);
    swapData[0] = abi.encode(PROTOCOL.DEX, paraSwapData);

    // keeper calls the runNextAction function.
    vm.prank(keeper);
    PerpetualVault(vault).runNextAction(prices, swapData);

    // flow is DEPOSIT
    uint8 flow = uint8(PerpetualVault(vault).flow());
    assertEq(flow, 1);

    address user2 = makeAddr("user2");

    // this will revert with FlowInProgress() error.
    vm.deal(user2, 10 ether);
    depositFixture(user2, 1e10);
  }
```

**Recommended Mitigation:** In [`PerpetualVault::_runSwap`](https://github.com/CodeHawks-Contests/2025-02-gamma/blob/84b9da452fc84762378481fa39b4087b10bab5e0/contracts/PerpetualVault.sol#L976) call the `_finalize` function to delete the flow, flowState and swapData


```diff
 function _runSwap(bytes[] memory metadata, bool isCollateralToIndex, MarketPrices memory prices) internal returns (bool completed) {
    ....

    else {

        ....

        if (flow == FLOW.DEPOSIT) {
            // last `depositId` equals with `counter` because another deposit is not allowed before previous deposit is completely processed
          _mint(counter, outputAmount + swapProgressData.swapped, true, prices);
+         _finalize(hex"");
        }

        ....

    }

    ....
 }
```

### [H-2] The `PerpetualVault::afterLiquidationExecution` function is missing the allocation for `isPositionClosed`.

**Description:** The `PerpetualVault::afterLiquidationExecution` function is triggered by the `gmxProxy` contract when a liquidation callback is received from `GMX`. However, the function fails to set `isPositionClosed` to `true` when a position is liquidated during the `deposit` or `withdraw` flow, potentially causing unintended protocol behavior.

**Impact:** When withdrawing collateral tokens, the protocol bypasses the `(positionIsClosed == true)` check, which can result in incorrect fund transfers or prevent users from retrieving their funds.

**Proof of Concept:** 
1. current state, beenLong == true, leverage == 10_000, positionIsClose == false
2. User calls the withdraw function, nextAction.selector is set to NextActionSelector.WITHDRAW_ACTION.
3. The position get liquidated and `afterLiquidationExecution` gets called.
4. as the current flow is withdraw so the `nextAction` stays the same.
5. keeper calls the `runNext` function which calls the `_withdraw` function
6. The _withdraw function checks if the `positionIsClosed` is true or false.
7. As the positionIsClosed is currently false and `_isLongOneLeverage(beenLong)` is true, causing  the protocol to place a `MarketSwap` order with swapAmount as 0 leading to an error from the GMX as the position has been liquidated.

**Recommended Mitigation:** Add a mechanism in the `afterLiquidationExecution` function to allocate `isPositionClose` to be true when the flow is `withdraw` and `deposit`.


## Medium
### [M-1] Excess executionFee not getting refunded to the users

**Description:** When a user deposits collateral tokens, they must pay an `executionFee` to create a GMX order based on their current position. The `_payExecutionFee` function ensures that `msg.value` is greater than `minExecutionFee` but does not enforce a maximum cap. This oversight can result in users' funds becoming stuck in the protocol

Similarly when user withdrawas collateral token and currentPositionKey is empty then user should not pay executionFee.

**Proof of Concept:**
1. current state, positionIsClosed == false.
2. user deposits collateral token with `msg.value` as `minExecutionFee + 1e10`
3. The keeper creates an Order for the user and mints the necessary shares
4. as an GMX order was placed so their would be no refund leading to user not getting their fee back.

**Impact:** Users won't be able to get their excess fee back from the protocol and might have to pay executionFee even when position is closed.

**Recommended Mitigation:** Implement a Maximum cap on the msg.value in `_payExecutionFee` function so that the fee payed by the user won't exceed a certain limit.

### [M-2] Wrong allocation of `executionFee` in `depositInfo`.

**Description:** User's order executionFee is stored in the `depositInfo[depositId].executionFee`, According to the protocol, users must pay an execution fee when a deposit or withdrawal triggers a GMX order execution; otherwise, the fee is refunded. The allocation of the execution fee is handled in the `PerpetualVault::_payExecutionFee` function, where, if `msg.value` exceeds `minExecutionFee`, the amount is transferred to the `gmxProxy` contract and stored as the execution fee. However, the execution fee calculation within `_payExecutionFee` is incorrect.

**Proof of Concept:**
1. current state, positionIsClose == false, beenLong == false, leverage == 10_000
2. User deposits `1e10` collateral token with `msg.value` as `2e18`.
3. The `depositInfo[depositId].executionFee` of user is stored as `1e18`.
4. Increase position order is placed on `GMX`.
5. User is minted corresponding shares based on the depsoited value without getting any refund on the deposit.
6. now, if the state changes to beenLong == true.
7. User calls the withdraw function with `msg.value` as `1e10`.
8. User pays the executionFee which will be overridden to `1e18`.
9. And when the user receives the remaning of the executionFee, the amount received by the user will be less than what they deposited.


**Proof of Concept(Coded):** Add this test to `PerpetualVault.t.sol` and run `forge test --mt test_executionFeeReturn --via-ir --rpc-url arbitrum -vvv`

```js
  function test_wrongTotalSharesCalculation() public {
    address alice = makeAddr("alice");
    payable(alice).transfer(1 ether);
    depositFixture(alice, 1e10);
    vm.deal(alice, 100 ether);

    MarketPrices memory prices = mockData.getMarketPrices();
    bytes[] memory data = new bytes[](2);
    data[0] = abi.encode(3380000000000000);
    address keeper = PerpetualVault(vault).keeper();
    vm.prank(keeper);
    PerpetualVault(vault).run(true, false, prices, data);
    GmxOrderExecuted(true);
    bytes[] memory metadata = new bytes[](0);
    vm.prank(keeper);
    PerpetualVault(vault).runNextAction(prices, metadata);

    IERC20 collateralToken = PerpetualVault(vault).collateralToken();
    uint256 amount = 1e12;
    deal(address(collateralToken), alice, amount);
    uint256 executionFee = PerpetualVault(vault).getExecutionGasLimit(true);
    vm.startPrank(alice);
    collateralToken.approve(vault, amount);

    // deposit collateral token when beenLong is false.
    PerpetualVault(vault).deposit{value: (executionFee * tx.gasprice) + 2 ether}(amount);
    vm.stopPrank();
    uint256 userBalanceBeforeMinting = address(alice).balance;

    uint8 flow = uint8(PerpetualVault(vault).flow());
    assertEq(flow, 1);
    (PerpetualVault.NextActionSelector selector, ) = PerpetualVault(vault).nextAction();
    assertEq(uint8(selector), 1);

    data[0] = abi.encode(3380000000000000);
    vm.prank(keeper);
    PerpetualVault(vault).runNextAction(prices, data);

    GmxOrderExecuted(true);
    // user's balance after minting shares
    uint256 userBalanceAfterMinting = address(alice).balance;

    // users balance before and after minting shares are equal.
    assert(userBalanceBeforeMinting == userBalanceAfterMinting);

    uint256[] memory depositIds = PerpetualVault(vault).getUserDeposits(alice);
    assertEq(depositIds.length, 2);
    console.log("ExecutionFee after deposit: ",PerpetualVault(vault).getUserExecutionFee(depositIds[1]));
    
    (selector, ) = PerpetualVault(vault).nextAction();
    assertEq(uint8(selector), 6);
    flow = uint8(PerpetualVault(vault).flow());
    assertNotEq(flow, 0);
    vm.prank(keeper);
    delete data;
    PerpetualVault(vault).runNextAction(prices, data);
    (selector, ) = PerpetualVault(vault).nextAction();
    assertEq(uint8(selector), 0);
    flow = uint8(PerpetualVault(vault).flow());
    assertEq(flow, 0);

    // withdraw collateral tokens when beenLong == true.

    depositIds = PerpetualVault(vault).getUserDeposits(alice);
    uint256 lockTime = PerpetualVault(vault).lockTime();
    vm.warp(block.timestamp + lockTime + 1);

    vm.prank(alice);
    PerpetualVault(vault).withdraw{value: (executionFee * tx.gasprice) + 1 ether}(alice, depositIds[1]);

    console.log("ExecutionFee after withdraw: ",PerpetualVault(vault).getUserExecutionFee(depositIds[1]));
  }
```

```sh
Logs:
      collateral/short/USDC amount:  10000000000
      indexToken/ETH amount 0
      ExecutionFee after deposit:  2000104667680000000
      ExecutionFee after withdraw:  3000209335360000000
```

**Impact:** This could lead to wrong allocation of executionFee leading to user loosing funds while claiming the the executionFee as a refund.

**Recommended Mitigation:** 

```diff
function _payExecutionFee(uint256 depositId, bool isDeposit) internal {
    uint256 minExecutionFee = getExecutionGasLimit(isDeposit) * tx.gasprice;

    if (msg.value < minExecutionFee) {
      revert Error.InsufficientAmount();
    }
    if (msg.value > 0) {
      payable(address(gmxProxy)).transfer(msg.value);
-     depositInfo[depositId].executionFee = msg.value;
+     depositInfo[depositId].executionFee += msg.value;
    }
  }
```


### [M-3] User should get redund when positionIsClosed.

**Description:** In `PerpetualVault::_withdraw` function when positionIsClose is true then the user gets their collateral token based on the current collateral balance of the protcol, If there is no GMX order execution the fee would be refunded and if there isn't the fee won't be refunded.
so the user should get their fee refunded if the postion is closed.

**Impact:** user not getting their executionFee refunded.

**Proof of Concept:** 

```solidity
function _withdraw(uint256 depositId, bytes memory metadata, MarketPrices memory prices) internal {
    ...

>>   if (positionIsClosed) {
      _handleReturn(0, true, false);
    } else if (_isLongOneLeverage(beenLong)) {  // beenLong && leverage == BASIS_POINTS_DIVISOR

      ...

    }
>>    else if (curPositionKey == bytes32(0)) {    // vault liquidated
      _handleReturn(0, true, false);
    } else {

        ....

    }
  }

```

**Recommended Mitigation:** 

```diff
function _withdraw(uint256 depositId, bytes memory metadata, MarketPrices memory prices) internal {
    ...

    if (positionIsClosed) {
-      _handleReturn(0, true, false);
+      _handleReturn(0, true, true);
    } else if (_isLongOneLeverage(beenLong)) {  // beenLong && leverage == BASIS_POINTS_DIVISOR

      ...

    }
    else if (curPositionKey == bytes32(0)) {    // vault liquidated
-      _handleReturn(0, true, false);
+      _handleReturn(0, true, true);
    } else {

        ....
        
    }
  }

```

## Low

### [L-1] leverage should have max cap of `50_000` in the initialize function

**Description:** The initialize function should have a max cap of `50_000` as `GMX` only allow leverage upto 50x. 

**Proof of Concept:** https://github.com/CodeHawks-Contests/2025-02-gamma/blob/84b9da452fc84762378481fa39b4087b10bab5e0/contracts/PerpetualVault.sol#L204

**Recommended Mitigation:** 
```diff
 function initialize(

    ...

    uint256 _leverage
  ) external initializer {

    ...

+   require(_leverage <= 50_000, '');    
    leverage = _leverage;
  }
```


### [L-2] Use `safeTransfer` or `transfer` in the whole `PerptualVault` contract.

**Description:** currently the `PerpetualVault` contract uses mix of both `safeTransfer` and `transfer` function to transfer ERC20 tokens, best practice is to use one of the in the same contract.

### [L-3] In `KeeperProxy::_validatePrice` the token price checks are wrong.

**Description:** The `KeeperProxy::_validatePrice` function checks the Chainlink price and the given price, but the checks for longToken is wrong.

**Impact:** Wrong checks leading to function always reverting.

**Proof of Concept:** https://github.com/CodeHawks-Contests/2025-02-gamma/blob/84b9da452fc84762378481fa39b4087b10bab5e0/contracts/KeeperProxy.sol#L176

**Recommended Mitigation:** 
```diff
function _validatePrice(address perpVault, MarketPrices memory prices) internal {
  
  ...
  
-   _check(marketData.longToken, prices.indexTokenPrice.min);
-   _check(marketData.longToken, prices.indexTokenPrice.max);
+   _check(marketData.longToken, prices.longTokenPrice.min);
+   _check(marketData.longToken, prices.longTokenPrice.max);

  ...

}
```

### [L-4] unused state variable.

**Description:** threshold in `KeeperProxy` is an unused state variable.

**Proof of Concept:** https://github.com/CodeHawks-Contests/2025-02-gamma/blob/84b9da452fc84762378481fa39b4087b10bab5e0/contracts/KeeperProxy.sol#L32
